apply plugin: 'java'
apply plugin: 'checkstyle'
apply plugin: 'findbugs'
apply plugin: 'jacoco'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'maven-publish'
apply plugin: 'project-report'

repositories {
    mavenCentral()
}

task beanstalkZip(type: Zip, dependsOn: build) {
    classifier 'beanstalk'

     from(projectDir) {
        include '.elasticbeanstalk/**'
        include 'Dockerfile'
        include 'run.sh'
        include "build/libs/${project.name}-*.jar"
    }
    from("$buildDir/tmp/ebextensions")
}

// Checkstyle
checkstyle {
    configFile = 'checkstyle.xml' as File
    toolVersion = '6.10.1'
}

tasks.withType(Checkstyle) {
  reports {
    html.destination rootProject.file("build/reports/checkstyle.html")
}
}

// Findbugs
tasks.withType(FindBugs) {
    excludeFilter = file("$rootDir/gradle/findbugs-exclude.xml")
    reports {
        xml.enabled = false;
        html.enabled = true;
    }
}

// Test coverage
test {
    jacoco {
        enabled = project.hasProperty("enableCodeCoverage")
    }
}

// Eclipse
task copyEclipseSettings(type: Copy) {
    from("${rootProject.projectDir}/gradle/ide/eclipse") {
        include "*.prefs"
    }
    into ".settings"
}
eclipseJdt.finalizedBy copyEclipseSettings
